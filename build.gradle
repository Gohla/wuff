buildscript {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
  }
}

ext {
  group = 'org.akhikhl.wuff'
  version = '0.0.6'
  description = 'Set of gradle plugins for developing and assembling Eclipse applications and plugins'
  groovy_version = '1.8.6'
  spock_version = '0.7-groovy-1.8'
  logback_version = '1.1.1'
  slf4j_version = '1.7.6'
}

apply plugin: 'base' // add "clean" task to the root project.

task('build')

task('wrapper', type: Wrapper) {
  gradleVersion = '1.11'
}

// we exclude buildExamples from main build,
// because building all examples might be too slow
task('buildExamples', type: GradleBuild) {
  dir = file('examples')
  tasks = [ 'build' ]
}

// we exclude cleanExamples from main build,
// because cleaning all examples might be too slow
task('cleanExamples', type: GradleBuild) {
  dir = file('examples')
  tasks = [ 'clean' ]
}

subprojects {
  if(project.parent.name != 'libs')
    return

  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }

  group = rootProject.ext.group
  version = rootProject.ext.version

  afterEvaluate {
    // lib projects should be always installed into "$HOME/.m2"
    project.tasks.build.finalizedBy project.tasks.install
    rootProject.tasks.buildExamples.dependsOn project.tasks.install
  }
}
